pro detectorplot,BF,DFi,DFo,nseg,aseg,maxang,multiple=multiple
;
; creates a drawing of a BF/HAADF detector, potentially
; segmented and rotated.  the routine also contains code
; to allow the user to select one or more segments for 
; which an HAADF image needs to be displayed.  Those segments
; will then be highlighted in the detector display.
;
; Written by MDG, OSU Sabbatical 6/14/2013
;
dim = 401
d2 = 200
dis = shift(dist(dim),d2,d2)

pixelradius = d2 

BFpixel = pixelradius * (BF/maxang)
DFipixel = pixelradius * (DFi/maxang)
DFopixel = pixelradius * (DFo/maxang)

; drawing will be in color
cimage = bytarr(3,dim,dim)

; bright field detector
BFdisk = replicate(0B,dim,dim)
BFdisk[where(dis le BFpixel)] = 250B

; dark field detector
DFdisk = replicate(0B,dim,dim)
DFdisk[where((dis gt DFipixel) and (dis le DFopixel))] = 250B

; if segmented, rotate and add the segment edges
w=2
if (nseg gt 1) then begin
  dtheta = 360.0/nseg
  for i=0,nseg do begin
    DFdisk = rot(DFdisk,i*dtheta,missing=0B,cubic=-0.5)
    DFdisk[d2-w:d2+w,d2+DFipixel-w:d2+DFopixel+w] = 0B
    DFdisk = rot(DFdisk,-i*dtheta,missing=0B,cubic=-0.5)
  endfor
; do we also need to offset the segmented detector angle ?
  if (aseg ne 0.0) then begin
    DFdisk = rot(DFdisk,aseg,missing=0B,cubic=-0.5)
  end

; next we must make a sequenced map of all sectors, using 
; the value 0 for the BF sector, and 1 .. nseg for the 
; dark field sectors, going clockwise; we'll use this 
; as a clickable map, so the user can click on a sector 
; to display the image generated by that sector
  DFmap = DFdisk gt 0
  lDFmap = label_region(DFdisk)
  clickablemap = lDFmap

; next, renumber the sectors
  rad = (DFopixel+DFipixel)/2
  thw = 360.0/float(nseg)/2.0
  th = aseg + thw + findgen(nseg) * dtheta
  c = cos((th-90.0)*!dtor)
  s = -sin((th-90.0)*!dtor)

  for i=0,nseg-1 do begin
    x = d2+c[i]*rad
    y = d2+s[i]*rad
    q = where(lDFmap eq lDFmap[x,y],cnt)
    if (cnt gt 0) then clickablemap[q] = i+1
  endfor

end else begin   ; detector is not segmented so the clickablemap is simple
  clickablemap = DFdisk gt 0
end



cimage[0,0:*,0:*] = BFdisk
cimage[1,0:*,0:*] = DFdisk

tvscl,cimage,true=1


; change color of the sector that the user selects
sel = 1
while (sel gt 0) do begin
  cursor,x,y,/dev,/down
  sel = clickablemap[x,y]
print,' sector : ',sel
  if (sel gt 0) then begin
    sector = replicate(0B,dim,dim)
    q = where(clickablemap eq sel)
    sector[q] = 255B
    cimage[2,0:*,0:*] = sector
    tvscl,cimage,true=1
  end
end





stop

end

